title: Start
---
<<set $knowsNarrator to false>>
<<set $pins to 90>>
Narrator: Hi, welcome to PAX Together 2022!
    -> Who are you?
        <<jump NarratorInfo>>
    -> Oh, hi. I recognise you.
        Narrator: You do?
            -> Yeah! You're me.
                <<jump NarratorRecognised>>
            -> Yeah! You're my imaginary friend, from when I was young!
                <<jump ImaginaryFriend>>
===

title: NarratorInfo
---
Narrator: Well, I guess... I can tell you everything.
    -> Go on...
        Narrator: Are you sure?
            -> ... yes.
                Narrator: How sure?
                    -> Just get on with it..
                        Narrator: Fine.
                        Narrator: I'm you.
                        <<if $knowsNarrator is true>>
                            Narrator: But you know that already.
                        <<endif>>
                            -> So how does that work?
                                Narrator: You from the future.
                                    -> Yeah, right.
                                        Narrator: It's me!
                                        Narrator: You!
                                        Narrator: I!
                                        Narrator: Us!
                                        <<set $knowsNarrator to true>>
                                            -> But why?
                                                <<jump ButWhy>>
    -> I know already. <<if $knowsNarrator is true>>
        <<jump WhatNow>>
===

title: NarratorRecognised
---
<<set $knowsNarrator to true>>
Narrator: Huh, OK. 
Narrator: Well, yes, that's true. 
Narrator: I am 
    -> Knew it.
        Narrator: How'd you know it?
            -> I've played this story before.
                Narrator: Oh, that makes sense.
                <<jump NarratorInfo>>
===

title: ImaginaryFriend
---
Narrator: Uh, yes. That's right. I am.
Narrator: Remember.. we used to play.. a..
Narrator: Guessing game?
    -> No we didn't.
        Narrator: uh..
            -> But I like games. Continue.
                Narrator: Great..
                Narrator: We played a dice.. guessing game.
                Narrator: I rolled a dice and you guessed the number.
                    -> A D6?
                        Narrator: Yeah..
                            -> OK, let's play it again.
                                Narrator: Really?
                                    -> Yeah, for "old times" sake.
                                        Narrator: haha.. hah. sure.
                                        <<jump DiceGame>>
===

title: DiceGame
---
<<declare $number = 0>>
<<set $number to dice(6)>>

Narrator: Guess a number...
    -> 1.
        <<if $number == 1>>
            Narrator: Whoa! Amazing, just like, uh, old times.
            Narrator: You got it.
                -> Yeah I did!
                    <<jump DoneWithGame>>
        <<endif>>
    -> It's 2.
            <<if $number == 2>>
            Narrator: Hey, nice. Yep!
                -> I'm just that good.
                    <<jump DoneWithGame>>
            <<endif>>
    -> 3! 3! 3!
        <<if $number == 3>>
            Narrator: Yeah, it was 3.
                -> I know..
                    <<jump DoneWithGame>>
        <<endif>>
    -> Hmm.. 4.
          <<if $number == 4>>
            Narrator: Incredible how you just did that! Right.
                -> It was always going to be 4.
                    <<jump DoneWithGame>>
          <<endif>>
    -> 5, I think.
        <<if $number == 5>>
            Narrator: You think right. 5 it is.
                -> Bam!
                    <<jump DoneWithGame>>
        <<endif>>
    -> 6. Definitely.
        <<if $number == 2>>
            Narrator: Definitely correct, too! Good job.
                -> Haha, I'm amazing..
                    <<jump DoneWithGame>>
        <<endif>>
    Narrator: Hah. Nope.
    <<jump DoneWithGame>>
===

title: DoneWithGame
---
Narrator: Right, well, that was fun.
Narrator: You've played the dice game {visited_count("DiceGame")} times.
    -> Again.
        Narrator: *sigh*
            <<jump DiceGame>>
    -> Fine. What was it you wanted?
        Narrator: Oh.. uh.
            -> So, who are you really?
                <<jump NarratorInfo>>
===

title: ButWhy
---
Narrator: We need your pins.
Narrator: Our pins.
    -> But I like my pins...
        Narrator: I know, I know.
            -> They're, like, one of the main reasons I come to PAX!
                Narrator: And the friends.
                    -> Well, yeah.
                        Narrator: Anyway, you've got like {$pins} of them! You can spare some. We can.
                            -> Hmm. Not so sure.
                                <<jump Nope>>
                            -> Fine, you're right.
                                <<jump Yep>>
===

title: WhatNow
---
Narrator: So, can I have them?
    -> My pins?
        Narrator: Yeah, you've got like {$pins} of them...
            -> Fine.
                <<jump Yep>>
            -> Nope.
                <<jump Nope>>
    -> You can have one.
        <<jump JustOne>>
===

title: Nope
---
Narrator: Uh, OK.
Narrator: The future literally depends on it, but sure, you keep them.
    -> I will.
        Narrator: Ok...
            -> Yep.
    -> I could...
        Narrator: ... yes?
            -> I could give you just one.
                Narrator: Yeah... OK.
                    <<jump JustOne>>
            -> Nevermind.
                Narrator: Fine, gosh I'm a jerk.
                    -> We're a jerk.
                        Narrator: Yes, that.
===

title: Yep
---
Narrator: OK, I'll take all {$pins} of them?
    -> Sure.
        Narrator: Done! Thanks. I'll be off.
        <<set $pins to 0>>
            -> That was totally worth it.
                Narrator: What was?
                    -> Having {$pins} pins.
                        Narrator: Well, you saved the future.
                            -> How?
                                Narrator: Can't spoil that for us.
    -> No, leave me with one, please?
        Narrator: Yeah, OK.
        <<set $tempPins to $pins - 1>>
        <<set $pins to 1>>
        Narrator: I've taken {$tempPins}, leaving you with {$pins}. OK?
            -> Yeah.
                Narrator: Thanks, mate!
===

title: JustOne
---
Narrator: I'll take it. Just one.
    -> Yep. You can even choose.
        Narrator: Thanks... 
        <<set $pins = $pins - 1>>
        Narrator: You've got {$pins} of them now, still, anyway.
            -> I know, baby!
                Narrator: *sigh*
                Narrator: Thanks, though.
                    -> Eh, it was nothing.
===